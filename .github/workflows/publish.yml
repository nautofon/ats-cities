---
name: Publish

on:
  workflow_dispatch:
  push:
    branches:
      - 'main'

jobs:
  typeset:
    runs-on: ubuntu-latest
    permissions:
      contents: read
      pages: write
      id-token: write
    environment:
      name: github-pages
      url: ${{ steps.deploy.outputs.page_url }}
    steps:
      - uses: actions/checkout@v6
      - uses: shogo82148/actions-setup-perl@v1
        with:
          perl-version: '5.40'
          install-modules-with: cpm

      - shell: perl {0}
        run: |
          use v5.36;
          use HTTP::Tiny;
          use JSON::MaybeXS;
          use Path::Tiny;
          use Text::CSV 'csv';
          use Text::Table::HTML;

          my $url = 'https://truckermudgeon.github.io/extra-labels.geojson';
          my $dir = path 'pages';

          my $response = HTTP::Tiny->new->get($url);
          $response->{success} or die "$url: $response->{status} $response->{reason}";
          my $json = decode_json $response->{content};

          # Temp fix: Add cities currently missing from extra-labels dataset
          push $json->{features}->@*,
            map {+{ properties => { kind => 'city', $_->%* } }} (
              { country => 'US-CA', text => 'Hilt',         city => 'hilt' },
              { country => 'US-ID', text => 'Ketchum',      city => 'ketchum' },
              { country => 'US-KS', text => 'Kansas City',  city => 'kansas_ci_ks' },
              { country => 'US-MO', text => 'Saint Joseph', city => 'st_joseph' },
              { country => 'US-MO', text => 'Saint Louis',  city => 'st_louis' },
              { country => 'US-TX', text => 'Abilene',      city => 'abilene' },
              { country => 'US-TX', text => 'Longview',     city => 'longview_tx' },
              { country => 'US-TX', text => 'Lubbock',      city => 'lubbock' },
              { country => 'US-TX', text => 'San Antonio',  city => 'san_antonio' },
              { country => 'US-TX', text => 'Texarkana',    city => 'texarkana' },
              { country => 'US-OR', text => 'Portland',     city => 'portland' },
              { country => 'US-WA', text => 'Grand Coulee', city => 'grand_coulee' },
            );

          my %cities;
          $cities{ $_->{city} // "$_->{country} $_->{text}" } = $_ for
            grep { $_->{kind} && $_->{kind} eq 'city' }
            map  { $_->{properties} }
            $json->{features}->@*;
          my @cities = sort {
            $a->{country} cmp $b->{country} or
            $a->{city}    cmp $b->{city}
          } map {+{
            country => $_->{country},
            city    => $_->{text},
            token   => $_->{city},
          }} values %cities;

          $dir->mkdir;
          $dir->child('ats-cities.json')->spew_raw(encode_json \@cities);

          my $header = [qw( country city token )];
          my @table = map {[
            $_->{country},
            $_->{city},
            $_->{token},
          ]} @cities;
          csv in => [$header, @table], out => "$dir/ats-cities.csv";

          # Get human-readable country/state names for HTML output
          my $countries = csv in => "countries.csv";
          my %countries;
          $countries{ $_->[0] } = $_ for $countries->@*;
          $_->[0] = $countries{ $_->[0] }->[1] for @table;
          @table = sort { $a->[0] cmp $b->[0] } @table;
          $header = ['State', 'City', 'Game Token'];

          # Verify that the city count for each country/state is as expected
          delete $countries{country};
          for my $country ( sort keys %countries ) {
            my $listed_cities   = grep { $_->{country} eq $country } @cities;
            my $expected_cities = $countries{$country}->[2];
            if ( $listed_cities != $expected_cities ) {
              warn sprintf "Expected %i cities for %s, found %i",
                $expected_cities, $country, $listed_cities;
            }
          }

          $dir->child('index.html')->spew_utf8(
            path('table.html')->slurp_utf8,
            sprintf("<p class=updated>Last updated: %s UTC\n\n", scalar gmtime),
            Text::Table::HTML::table(
              rows => [$header, @table],
              header_row => 1,
            ),
          );

      - uses: actions/configure-pages@v5
      - uses: actions/upload-pages-artifact@v4
        with:
          path: 'pages'
      - uses: actions/deploy-pages@v4
        id: deploy
